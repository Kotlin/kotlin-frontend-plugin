plugins {
    id "java"
    id "java-gradle-plugin"
    id "maven-publish"
    id "org.jetbrains.kotlin.jvm" version "1.3.21"
    id "com.jfrog.bintray"
    id 'net.researchgate.release'
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile gradleTestKit()

    compile "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

//    compile group: 'net.rubygrapefruit', name: 'native-platform', version: '0.10'

    testCompile 'junit:junit:4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

defaultTasks 'jar'

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mainMaven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar

            groupId project.group
            artifactId project.name + "-plugin"
            version project.version
        }
        
    }
}

bintray {
    user = System.getProperty('bintray.user.name') ?: ""
    key = System.getProperty('bintray.user.token') ?: ""
    publish = true

    publications = ['mainMaven'] 

    pkg {
        repo = 'kotlin-eap'
        name = 'kotlin-frontend'
        userOrg = 'kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Kotlin/kotlin-frontend-plugin.git'

        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload

